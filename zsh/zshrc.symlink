# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

autoload -U promptinit; promptinit
prompt pure
export GOPATH=/usr/local/go
export PATH="$(brew --prefix php71)/bin:/usr/local/bin:$PATH:$GOROOT/bin:$GOPATH/bin"
alias ssh='assh wrapper ssh'

ssh-add

eval $(cachalot env)

# Path to WP_I18N_LIB
export WP_I18N_LIB="/Users/paschi/Development/tools/wpi18n"

eval "$(rbenv init -)"